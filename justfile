[private]
default:
    @just --list --unsorted

supported_packaging_systems := "uv nix"
has_nix := `command -v nix`

[private]
ensure_nix:
    #!/usr/bin/env sh
    if [ -z "{{ has_nix }}" ]; then
        echo "‚ùå Nix must be installed";
        exit 1;
    fi;

[private]
build_nix: ensure_nix
    @echo "üì¶Ô∏è Packaging using nix..."
    @nix build --no-link

[private]
build_uv:
    @echo "üì¶Ô∏è Packaging using uv..."
    @uv build

# Build project
[group('build')]
build *system='uv':
    #!/usr/bin/env sh
    if echo "{{ supported_packaging_systems }}" | grep -qw "{{ system }}"; then
        system_target="build_{{ system }}";
        just "$system_target";
    else
        echo "‚ùå Unsupported packaging system: {{ system }}";
        exit 1;
    fi;

# Run formatters
[group('misc')]
fmt:
    #!/usr/bin/env sh
    uv run ruff format src tests;
    uv run ruff check src tests --fix;

    if [ -n "{{ has_nix }}" ]; then
        nix fmt flake.nix nix
    fi;

# Display flake schema
[group('misc')]
schema: ensure_nix
    @nix flake show

# Run tests
[group('misc')]
test:
    @uv run --frozen --no-default-groups pytest tests
